plugins {
    id 'eclipse' // optional (to generate Eclipse project files)
    id 'idea' // optional (to generate IntelliJ IDEA project files)

    id 'java'

    id 'com.diffplug.spotless' version '6.22.0'
}

group 'de.renatius.tdd-playground'
version '1.0.0-SNAPSHOT'

allprojects {
    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'eclipse'
    apply plugin: 'idea'

    plugins.withId('java') {
        apply plugin: 'checkstyle'

        apply plugin: 'com.diffplug.spotless'

        dependencies {
            testImplementation(
                    enforcedPlatform(libs.junit.bom.get()),
                    enforcedPlatform(libs.mockito.bom.get()),
            )
        }

        test {
            useJUnitPlatform()

            testLogging {
                events 'passed', 'skipped', 'failed'
            }
        }
    }
}

spotless {
    java {
        importOrder()
        removeUnusedImports()
        cleanthat()
        googleJavaFormat()
        formatAnnotations()
    }
}

tasks.register('publish', Copy) {
    dependsOn ':tipps:asciidoctor'

    from project(':tipps').layout.buildDirectory.dir('docs/asciidoc')
    into layout.projectDirectory.dir('docs')
}
